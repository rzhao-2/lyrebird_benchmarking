
import extern
import os
import sys
sys.path.append("..")
# from app import object

from tool_reference_data import *

num_threads = config['benchmarking_threads']
kraken_num_threads = 999 # never let kraken be parallelised, because otherwise it will exhaust the RAM.

tools = ['lyrebird']

output_prefix = 'output_'
output_dirs = list([output_prefix+tool for tool in tools])
output_dirs_dict = dict(zip(tools, output_dirs))

benchmark_dir = 'benchmarks'

datasets = list(['marine'+str(i) for i in range(10)])
# datasets = ["marine10"]

fastq_dir = 'local_reads'
truth_dir = os.path.join(workflow.basedir, 'truths')
#TODO: select genome paths
genome_fasta_paths = 'genome_rep_paths.csv'
coverage_definitions_folder = 'coverage_definitions_2'

# Restrict to r207-consistent tools
tools = r207_tools

# debug
# tools = ['singlem']
# datasets = [datasets[6]] # debug

##################################################################### things that should be in config

# gtdb_bac_metadata = '../bac120_metadata_r207.tsv'
# gtdb_ar_metadata = '../ar53_metadata_r207.tsv'
viral_metadata = '../viral_metadata_reconstructed.tsv'


##################################################################### reference databases

# singlem_metapackage = "/work/microbiome/msingle/rossenzhao/phrogs_v4.1/0.2/metapackage/phrog4.1_v0.2.2024_11_7.smpkg/"
singlem_metapackage = "/mnt/hpccs01/work/microbiome/msingle/rossenzhao/phrogs_v4.1/0.3/metapackage/lyrebird_v0.3.1_phrog_v4.1_20250720.smpkg"
# metapackage = '/work/microbiome/msingle/mess/163_S3.1.1_metapackage/output_dir/metapackage/S3.1.1.GTDB_r207.metapackage_20240302.smpkg'
# singlem_metapackage_local = join(output_dirs_dict['singlem'], 'data', os.path.basename(singlem_metapackage))

#####################################################################


rule all:
    input:
        expand(output_prefix+"{tool}/opal/{sample}.opal_report", sample=datasets, tool=tools)

rule generate_community_and_reads:
    output:
        r1=fastq_dir + "/{sample}.1.fq.gz",
        r2=fastq_dir + "/{sample}.2.fq.gz",
        condensed = truth_dir + "/{sample}.condensed",
        genomewise = truth_dir + "/{sample}.genomewise.csv",
        done = touch(truth_dir + "/{sample}.finished"),
        done2 = touch(fastq_dir + "/{sample}.finished"),
    params:
        coverage_number = lambda wildcards: wildcards.sample.replace('marine', ''),
    threads: num_threads
    conda:
        'envs/art.yml'
    shell:
        "{workflow.basedir}/generate_community.py --art art_illumina --threads {threads} --coverage-file {workflow.basedir}/coverage_definitions_2/coverage{params.coverage_number}.tsv --viral-metadata {viral_metadata} --genome-list {genome_fasta_paths} --output-condensed {output.condensed} -1 {fastq_dir}/{wildcards.sample}.1.fq.gz -2 {fastq_dir}/{wildcards.sample}.2.fq.gz --output-genomewise-coverage {output.genomewise}"

rule truth_condensed_to_biobox:
    input:
        condensed = truth_dir + "/{sample}.condensed",
    output:
        biobox = truth_dir + "/{sample}.condensed.biobox",
    conda:
        "envs/singlem.yml"
    shell:
        "{workflow.basedir}/../bin/condensed_profile_to_biobox.py --input-condensed-table {input.condensed} " \
        "--output-biobox {output.biobox}"

rule tool_condensed_to_biobox:
    input:
        profile = output_prefix + "{tool}/{tool}/{sample}.profile",
        truth = truth_dir + "/{sample}.condensed.biobox",
    output:
        biobox = output_prefix + "{tool}/biobox/{sample}.biobox",
    conda:
        "envs/singlem.yml"
    shell:
        "{workflow.basedir}/../bin/condensed_profile_to_biobox.py --input-condensed-table {input.profile} " \
        "--output-biobox {output.biobox} --template-biobox {input.truth} "

rule opal:
    input:
        biobox = output_prefix+"{tool}/biobox/{sample}.biobox",
        truth = truth_dir + "/{sample}.condensed.biobox",
    params:
        output_dir = output_prefix+"{tool}",
        output_opal_dir = output_prefix+"{tool}/opal/{sample}.opal_output_directory",
    output:
        report=output_prefix+"{tool}/opal/{sample}.opal_report",
        done=output_prefix+"{tool}/opal/{sample}.opal_report.done",
    conda:
        'envs/opal.yml'
    shell:
        "opal.py -g {input.truth} -o {params.output_opal_dir} {input.biobox} || echo 'expected opal non-zero existatus'; mv {params.output_opal_dir}/results.tsv {output.report} && rm -rf {params.output_opal_dir} && touch {output.done}"


###############################################################################################
###############################################################################################
rule lyrebird_run_to_profile:
    input:
        r1=fastq_dir + "/{sample}.1.fq.gz",
        r2=fastq_dir + "/{sample}.2.fq.gz",
        db=singlem_metapackage,
    benchmark:
        benchmark_dir + "/lyrebird/{sample}-"+str(num_threads)+"threads.benchmark"
    output:
        report=output_dirs_dict['lyrebird'] + "/lyrebird/{sample}.profile",
        otu_table = output_dirs_dict['lyrebird'] + "/lyrebird/{sample}.otu_table",
        done=touch(output_dirs_dict['lyrebird'] + "/lyrebird/{sample}.profile.done")
    conda:
        "singlem-v0.19.0"
    threads:
        num_threads
    resources:
        mem_mb=8 * 1024,
    log:
        output_dirs_dict['lyrebird'] + "/logs/lyrebird/{sample}.log"
    shell:
        "lyrebird pipe --threads {threads} -1 {input.r1} -2 {input.r2} -p {output.report} --otu-table {output.otu_table} --output-extras --metapackage {input.db} &> {log}"